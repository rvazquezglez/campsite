import java.text.SimpleDateFormat

plugins {
	id 'org.springframework.boot' version '2.2.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'org.asciidoctor.convert' version '1.5.8'
	id 'java'
}

group = 'com.upgrade'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot.experimental:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.flywaydb:flyway-core'
	implementation 'com.github.ben-manes.caffeine:caffeine:2.8.0'
	runtimeOnly 'com.h2database:h2:1.4.199'
	runtimeOnly 'io.r2dbc:r2dbc-h2'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.boot.experimental:spring-boot-test-autoconfigure-r2dbc'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'
	testImplementation 'org.flywaydb.flyway-test-extensions:flyway-spring-test:6.0.0'
	testImplementation 'org.assertj:assertj-core:3.14.0'
}

dependencyManagement {
	imports {
		mavenBom 'org.springframework.boot.experimental:spring-boot-bom-r2dbc:0.1.0.M2'
	}
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	attributes "snippets": snippetsDir,
		"version": version,
		"linkcss": true,
		"date": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
	inputs.dir snippetsDir
	dependsOn test
	sourceDir "src/main/asciidoc"
	outputDir "build/docs"
	doLast {
		copy {
			from 'build/docs/html5'
			into "$projectDir/docs"
//			include 'index.html'
		}
	}
}

build.dependsOn asciidoctor
